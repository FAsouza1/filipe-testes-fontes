#include 'TOTVS.CH'
#include 'Protheus.ch'


/*/{Protheus.doc} Projeto01
Cria uma tela funcional com 3 abas e diversos elementos visuais
@type function
@version 
@author Placido
@since 03/10/2020
@return return_type, return_description
@see https://tdn.totvs.com/pages/viewpage.action?pageId=203771190
@see https://tdn.totvs.com/display/tec/TBitmap
/*/
User Function  Projeto01()

	Private oDlg1
	Private cTituloJanela := "PROJETO01- Primeira tela em ADVPL Protheus"

//Declarar as variáveis do Folder/Pasta
	Private oFolder
	Private aFolder

/*%%%%%%%%%%%%%%%%%%%%%%%%%%%%
Variáveis referentes aos objetos do FOLDER01(DADOSPESSOAIS)
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%*/
Private oSayNome
Private oGetNome
Private cGetNome

Private oSayNomeReduz
Private oGetNomeReduz
Private cGetNomeReduz

Private oSayDatNasc
Private oGetDatNasc
Private dGetDatNasc //Variável será tipo Data/Date

Private oSayDDDCel
Private oGetDDDCel
Private cGetDDDCel

Private oSayCEL
Private oGetCEL
Private cGetCEL

Private oGroupSexo //Agrupador de elementos que receberá os RadioButtons de Sexo

Private aSexo := {"Masculino","Feminino"}
Private nSexo  := 2
Private oRadioSexo

Private oSayCPf
Private oGetCPF
Private cGetCPF

Private oButtonAba01

/*%%%%%%%%%%%%%%%%%%%%%%%%%%%%
Variáveis referentes aos objetos do FOLDER02(ENDEREÇO)
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%*/
Private oSay9
Private oGet9
Private cGet9

Private oSay10
Private oGet10
Private cGet10

Private oSay11
Private aImoveis       := {"Casa","Apartamento","Outro"}
Private cImovel        := aImoveis[2]
Private oComboImovel

Private oSay12
Private oGet11
Private cGet11

Private oSay13
Private aEstados      := {"AC", "AL", "AM", "AP", "BA", "CE", "DF", "ES", "GO", "MA", "MT", "MS", "MG", "PA", "PB", "PR", "PE", "PI", "RJ", "RN", "RO", "RS", "RR", "SC", "SE", "SP", "TO"}
Private cEstados      := aEstados[5]
Private oComboEstado

Private oSay14
Private oGet12
Private cGet12

Private oButtonAba02
/*%%%%%%%%%%%%%%%%%%%%%%%%%%%%
/Variáveis referentes aos objetos do FOLDER03(OUTROS)
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%*/
Private oSayEstCivil
Private aEstCivil := {"Solteiro","Namorando","Casado","Protheuzando","Codificando","Vagando na net","Só no Insta","O pai tá on","Casado com Protheus","Namorando ADVPL"}
Private cEstCivil  := aEstCivil[4]
Private oComboEstCivil

Private oGroupPet

Private aPet := {"Sim","Não"}
Private nPet  := 2
Private oRadioPet

Private oGroupLinguagens

Private oCheckAdvpl
Private oCheckSQL
Private oCheckCSHARP
Private oCheckPython

Private lCheckAdvpl   := .F.
Private lCheckSQL     := .F.    
Private lCheckCSHARP  := .F.    
Private lCheckPython  := .F.  

Private oButtonMarkAll
Private oButtonUnMarkAll

Private oSayObservacao
Private cObservacao
Private oTMultigetObservacao

Private oButtonAba03

/*%%%%%%%%%%%%%%%%%%%%%%%%%%%%
Variáveis de elementos que estão fora das ABAS/FOLDER
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%*/
Private oButtonIncluir
Private oButtonAlterar
Private oButtonConsultar
Private oButtonExcluir


Private oBmpIncluir
Private oBmpAlterar
Private oBmpConsultar
Private oBmpExcluir


/*%%%%%%%%%%%%%%%%%%%%%%%%%%%%
COMEÇO À CONSTRUIR A TELA DAQUI PARA BAIXO
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%*/
oDlg1       := TDialog():New(0,0,720,1280,cTituloJanela,,,,,CLR_BLACK,CLR_WHITE,,,.T.)

aFolder := {"Dados Pessoais","Endereço","Outros"}
oFolder := TFolder():New(0,0,aFolder,aFolder,oDlg1,,,, .T.,, 600,200 )

/*%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
Elementos visuais que compõem o FOLDER/PASTA01 DADOS PESSOAIS
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%*/
oSayNome        := TSay():New(01,05,{||'Nome'},oFolder:aDialogs[1],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,20)
cGetNome        := Space(30)
oGetNome        := TGet():New( 10,05,{|u| IIF(Pcount()>0,cGetNome:=u,cGetNome)},oFolder:aDialogs[1],096,009,"",,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,cGetNome,,,, )

oSayNomeReduz   := TSay():New(01,110,{||'Nome Reduzido'},oFolder:aDialogs[1],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,20)
cGetNomeReduz   := Space(30)
oGetNomeReduz   := TGet():New( 10,110,{|u| IIF(Pcount()>0,cGetNomeReduz:=u,cGetNomeReduz)},oFolder:aDialogs[1],60,009,"",,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,cGetNomeReduz,,,, )

oSayDatNasc     := TSay():New(01,180,{||'Data de Nascimento'},oFolder:aDialogs[1],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,20)
dGetDatNasc     := Date() //Função que traz a data atual
oGetDatNasc     := TGet():New( 10,180,{|u|if(PCount()>0,dGetDatNasc:=u,dGetDatNasc)},oFolder:aDialogs[1],60,009,,,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,/**/,,,,.T. )

oSayDDDCel      := TSay():New(01,250,{||'DDD Cel.'},oFolder:aDialogs[1],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,20)
cGetDDDCel      := Space(4)
oGetDDDCel      := TGet():New( 10,250,{|u| IIF(Pcount()>0,cGetDDDCel:=u,cGetDDDCel)},oFolder:aDialogs[1],20,009,"@E (99)",,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,cGetDDDCel,,,, )

oSayCEL         := TSay():New(01,280,{||'Celular'},oFolder:aDialogs[1],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,20)
cGetCEL         := Space(11)
oGetCEL         := TGet():New( 10,280,{|u| IIF(Pcount()>0,cGetCEL:=u,cGetCEL)},oFolder:aDialogs[1],40,009,"@E 9-9999-9999",,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,cGetCEL,,,, )

oGroupSexo      := tGroup():New(01,330,50,430,"Sexo",oFolder:aDialogs[1],,,.T.) //Preste atenção no dimensionamento/tamanho do elemento, este é diferente dos outros

oRadioSexo      := tRadMenu():New(10,340,aSexo,{|u| iif(PCount()>0,nSexo :=u,nSexo )},oFolder:aDialogs[1],,,,,,,,60,20,,,,.T.)

oSayCPf    := TSay():New(25,05,{||'CPF'},oFolder:aDialogs[1],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,20)
cGetCPF    := Space(14)
oGetCPF    := TGet():New( 35,05,{|u| IIF(Pcount()>0,cGetCPF:=u,cGetCPF)},oFolder:aDialogs[1],096,009,"@E 999.999.999-99",,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,cGetCPF,,,, )

oButtonAba01      := TButton():New(60, 20, "VALIDAR DADOS PESSOAIS",    oFolder:aDialogs[1],{|| ShowDadosPessoais()}, 100,15,,,.F.,.T.,.F.,,.F.,,,.F. ) 


/*%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
Elementos visuais que compõem o FOLDER/PASTA02 ENDEREÇO
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%*/
oSay9    := TSay():New(01,05,{||'Endereço'},oFolder:aDialogs[2],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,20)
cGet9    := Space(40)
oGet9    := TGet():New( 10,05,{|u| IIF(Pcount()>0,cGet9:=u,cGet9)},oFolder:aDialogs[2],096,009,"",,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,cGet9,,,, )

oSay10      := TSay():New(01,110,{||'Bairro'},oFolder:aDialogs[2],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,20)
cGet10      := Space(30)
oGet10      := TGet():New( 10,110,{|u| IIF(Pcount()>0,cGet10:=u,cGet10)},oFolder:aDialogs[2],60,009,"",,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,cGet10,,,, )

oSay11      := TSay():New(01,180,{||'Tipo de Imóvel'},oFolder:aDialogs[2],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,20)
oComboImovel    := tComboBox():New(010,180,{|u|if(PCount()>0,cImovel:=u,cImovel)},aImoveis,60,20,oFolder:aDialogs[2],,,,,,.T.,,,,,,,,,'cImovel')

oSay12         := TSay():New(01,250,{||'CEP'},oFolder:aDialogs[2],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,20)
cGet11         := Space(9)
oGet11         := TGet():New( 10,250,{|u| IIF(Pcount()>0,cGet11:=u,cGet11)},oFolder:aDialogs[2],30,009,"@E 99999-999",,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,cGet11,,,, )

oSay13      := TSay():New(25,05,{||'Estado'},oFolder:aDialogs[2],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,20)
oComboEstado    := tComboBox():New(35,05,{|u|if(PCount()>0,cEstados:=u,cEstados)},aEstados,40,20,oFolder:aDialogs[2],,,,,,.T.,,,,,,,,,'cEstado')

oSay14      := TSay():New(25,50,{||'Cidade'},oFolder:aDialogs[2],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,20)
cGet12      := Space(30)
oGet12      := TGet():New( 35,50,{|u| IIF(Pcount()>0,cGet12:=u,cGet12)},oFolder:aDialogs[2],096,009,"",,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,cGet12,,,, )

oButtonAba02    := TButton():New(60, 20, "VALIDAR ENDEREÇO",    oFolder:aDialogs[2],{|| ShowEndereco()}, 100,15,,,.F.,.T.,.F.,,.F.,,,.F. ) 

/*%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
Elementos visuais que compõem o FOLDER/PASTA03 OUTROS
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%*/

oSayEstCivil       := TSay():New(01,05,{||'Estado Civil'},oFolder:aDialogs[3],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,20)
oComboEstCivil     := tComboBox():New(10,05,{|u|if(PCount()>0,cEstCivil:=u,cEstCivil)},aEstCivil,65,20,oFolder:aDialogs[3],,/**/,,,,.T.,,,,,,,,,'cCombo')

oGroupPet          := tGroup():New(01,80,40,140,"Possui Pet?",oFolder:aDialogs[3],,,.T.)
oRadioPet          := tRadMenu():New(10,85,aPet,{|u| iif(PCount()>0,nPet :=u,nPet )},oFolder:aDialogs[3],,,,,,,,30,20,,,,.T.)

oGroupLinguagens   := tGroup():New(01,150,55,250,"Linguagens que domina:",oFolder:aDialogs[3],,,.T.)

oCheckADVPL         := TCheckBox():New(10,160,'ADVPL',{|| lCheckADVPL},oFolder:aDialogs[3],100,210,,{|| lCheckADVPL := !lCheckADVPL},,,,,,.T.,,,)

oCheckSQL           := TCheckBox():New(20,160,'SQL',,oFolder:aDialogs[3],100,210,,,,,,,,.T.,,,)
oCheckSQL:bSetGet   := {|| lCheckSQL}
oCheckSQL:bLClicked := {|| lCheckSQL := !lCheckSQL}

oCheckCSHARP            := TCheckBox():New(30,160,'CSharp',,oFolder:aDialogs[3],100,210,,,,,,,,.T.,,,)
oCheckCSHARP:bSetGet    := {|| lCheckCSHARP}
oCheckCSHARP:bLClicked  := {|| lCheckCSHARP := !lCheckCSHARP}

oCheckPython            := TCheckBox():New(40,160,'Python',,oFolder:aDialogs[3],100,210,,,,,,,,.T.,,,)
oCheckPython:bSetGet    := {|| lCheckPython}
oCheckPython:bLClicked  := {|| lCheckPython := !lCheckPython}

oButtonMarkAll   := TButton():New(15, 200, "Todas", oFolder:aDialogs[3],;
{|| lCheckADVPL := .T.,lCheckSQL := .T., lCheckCSHARP := .T., lCheckPython := .T.}, 40,10,,,.F.,.T.,.F.,,.F.,,,.F. ) 

oButtonUnMarkAll   := TButton():New(30, 200, "Nenhuma", oFolder:aDialogs[3],;
{|| lCheckADVPL := .F.,lCheckSQL := .F., lCheckCSHARP := .F., lCheckPython := .F.}, 40,10,,,.F.,.T.,.F.,,.F.,,,.F. ) 

oSayObservacao       := TSay():New(01,270,{||'Observacao'},oFolder:aDialogs[3],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,20)
cObservacao          := "Digite aqui as características de Protheuzeiro que te definem"
oTMultigetObservacao := TMultiget():New(10,270, {|u|if(pCount()>0,cObservacao := u, cObservacao) }, oFolder:aDialogs[3], 200, 100, , , , , , .T. )

oButtonAba03      := TButton():New(60, 20, "VALIDAR OUTROS",    oFolder:aDialogs[3],{|| ShowOutros()}, 100,15,,,.F.,.T.,.F.,,.F.,,,.F. ) 


/*%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
Elementos visuais FORA DO FOLDER/ABAS
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%*/

oBmpIncluir := TBitmap():New(220,30,142,142,,"C:\TOTVS12127\Protheus\protheus_data\system\incluirprotheuzeiro.bmp",.T.,oDlg1,{||Alert("Clicou em ImagemIncluir")},,.F.,.F.,,,.F.,,.T.,,.F.)
oBmpIncluir:lAutoSize := .T.

oBmpAlterar := TBitmap():New(220,90,142,142,,"C:\TOTVS12127\Protheus\protheus_data\system\alterarprotheuzeiro.bmp",.T.,oDlg1,{||Alert("Clicou em ImagemAlterar")},,.F.,.F.,,,.F.,,.T.,,.F.)
oBmpAlterar:lAutoSize := .T.

oBmpConsultar := TBitmap():New(220,150,142,142,,"C:\TOTVS12127\Protheus\protheus_data\system\consultarprotheuzeiro.bmp",.T.,oDlg1,{||Alert("Clicou em ImagemConsultar")},,.F.,.F.,,,.F.,,.T.,,.F.)
oBmpConsultar:lAutoSize := .T.

oBmpExcluir := TBitmap():New(220,210,142,142,,"C:\TOTVS12127\Protheus\protheus_data\system\excluirprotheuzeiro.bmp",.T.,oDlg1,{||Alert("Clicou em ImagemExcluir")},,.F.,.F.,,,.F.,,.T.,,.F.)
oBmpExcluir:lAutoSize := .T.

oButtonIncluir      := TButton():New(250, 20, "Incluir",    oDlg1,{|| }, 40,15,,,.F.,.T.,.F.,,.F.,,,.F. ) 
oButtonAlterar      := TButton():New(250, 80, "Alterar",    oDlg1,{|| }, 40,15,,,.F.,.T.,.F.,,.F.,,,.F. ) 
oButtonConsultar    := TButton():New(250, 140, "Consultar", oDlg1,{|| }, 40,15,,,.F.,.T.,.F.,,.F.,,,.F. ) 
oButtonExcluir      := TButton():New(250, 200, "Excluir",   oDlg1,{|| }, 40,15,,,.F.,.T.,.F.,,.F.,,,.F. ) 


oDlg1:Activate(,,,.T.)

return


/*/{Protheus.doc} ShowDadosPessoais
Exibe o Conteúdo inserido na aba DADOSPESSOAIS
@type function
@version 
@author Placido
@since 03/10/2020
@return return_type, return_description
/*/
Static Function ShowDadosPessoais()

	MsgInfo("Nome:............."+cGetNome+Chr(13)+Chr(10)+;
		"Nome Reduzido:...."+cGetNomeReduz+Chr(13)+Chr(10)+;
		"Nascimento:......."+DToc(dGetDatNasc)+Chr(13)+Chr(10)+;
		"Telefone:........."+cGetDDDCel+"-"+cGetCEL+Chr(13)+Chr(10)+;
		"Sexo:............."+aSexo[nSexo]+Chr(13)+Chr(10)+;
		"CPF:.............."+cGetCPF,"ABA DADOS PESSOAIS")

return


/*/{Protheus.doc} ShowEndereco
Exibe o conteúdo inserido na aba ENDEREÇO
@type function
@version 
@author Placido
@since 03/10/2020
@return return_type, return_description
/*/
Static Function ShowEndereco()

	MsgInfo("Endereço:....."+cGet9+Chr(13)+Chr(10)+;
		"Bairro:......."+cGet10+Chr(13)+Chr(10)+;
		"Imovel:......."+cImovel+Chr(13)+Chr(10)+;
		"CEP:.........."+cGet11+Chr(13)+Chr(10)+;
		"Estado:......."+cEstados+Chr(13)+Chr(10)+;
		"Cidade:......."+cGet12,"ABA ENDERECO")

return


/*/{Protheus.doc} ShowOutros
Mostra o conteúdo inserido na aba OUTROS
@type function
@version 
@author Placido
@since 03/10/2020
@return return_type, return_description
/*/
Static Function ShowOutros()

	MsgInfo("Estado Civil:...."+cEstCivil+Chr(13)+Chr(10)+;
		"Possui Pet:......"+aPet[nPet]+Chr(13)+Chr(10)+;
		"ADVPL?:.........."+IIF(lCheckADVPL = .T.,"Sim","Não")+Chr(13)+Chr(10)+;
		"SQL?:............"+IIF(lCheckSQL,"Sim","Não")+Chr(13)+Chr(10)+;
		"CSHARP?:........."+IIF(lCheckCSHARP,"Sim","Não")+Chr(13)+Chr(10)+;
		"PYTHON?:........."+IIF(lCheckPython,"Sim","Não")+Chr(13)+Chr(10)+;
		"Observacao:......"+cObservacao,"ABA OUTROS")
return
